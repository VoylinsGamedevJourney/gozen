#!/usr/bin/env python
import platform as os_platform

LIBS_COMMON = [
    "avformat",
    "avcodec",
    "swscale",
    "swresample",
    "avutil"]

env = SConscript("godot_cpp/SConstruct")

env_suffix = env["suffix"]
env_shlibsuffix = env["SHLIBSUFFIX"]

jobs = ARGUMENTS.get("jobs", 4)
platform = ARGUMENTS.get("platform", "linux")
arch = ARGUMENTS.get("arch", "x86_64")
target = ARGUMENTS.get("target", "template_debug").split("_")[-1]
libpath = f"../bin/{platform}_{arch}/libgozen{env_suffix}{env_shlibsuffix}"

use_mingw = ARGUMENTS.get("use_mingw", "no")
use_system = ARGUMENTS.get("use_system", "no")


if arch == "arm64":
    env["CC"] = "aarch64-linux-gnu-gcc"
    env["CXX"] = "aarch64-linux-gnu-g++"
    env["LINK"] = "aarch64-linux-gnu-g++"


env.Append(
    CPPPATH=[
        f"ffmpeg/bin_{platform}/include",
        f"ffmpeg/bin_{platform}/aom/include",
        f"ffmpeg/bin_{platform}/svtav1/include",
        f"ffmpeg/bin_{platform}/x264/include",
        f"ffmpeg/bin_{platform}/x265/include",
        f"ffmpeg/bin_{platform}/mp3lame/include",
        f"ffmpeg/bin_{platform}/ogg/include",
        f"ffmpeg/bin_{platform}/opus/include",
        f"ffmpeg/bin_{platform}/vorbis/include",
        f"ffmpeg/bin_{platform}/vpx/include",
    ],
    LIBPATH=[
        f"ffmpeg/bin_{platform}/lib",
        f"ffmpeg/bin_{platform}/aom/lib",
        f"ffmpeg/bin_{platform}/svtav1/lib",
        f"ffmpeg/bin_{platform}/x264/lib",
        f"ffmpeg/bin_{platform}/x265/lib",
        f"ffmpeg/bin_{platform}/mp3lame/lib",
        f"ffmpeg/bin_{platform}/ogg/lib",
        f"ffmpeg/bin_{platform}/opus/lib",
        f"ffmpeg/bin_{platform}/vorbis/lib",
        f"ffmpeg/bin_{platform}/vpx/lib",
    ],
)


if "linux" in platform and use_system == "yes":
    env.Append(
        CPPPATH=["/usr/include/ffmpeg/"],
        LIBS=LIBS_COMMON,
    )
elif "linux" in platform:
    march_flags = {"x86_64": "x86-64", "arm64": "armv8-a"}

    libs = [
        "SvtAv1Enc",
        "aom",
        "mp3lame",
        "opus",
        "vorbis",
        "vorbisenc",
        "ogg",
        "vpx",
        "x264",
        "x265",
        "numa",
        "va",
        "va-drm",
        "va-x11",
        "vdpau",
        "X11",
        "m",
        "pthread",
        "dl",
    ]

    env.Append(
        LINKFLAGS=["-static-libstdc++", "-fPIC", "-Wl,-Bsymbolic"],
        CCFLAGS=[f"-march={march_flags[arch]}"],
        LIBS=LIBS_COMMON + libs,
    )

    if arch != "arm64":
        env.Append(LIBS=["z", "bz2", "lzma"])

elif "windows" in platform:
    if os_platform.system().lower() == "windows" and use_mingw != "yes":
        env.Append(
            LIBS=[
                "avcodec.lib",
                "avformat.lib",
                "avutil.lib",
                "swresample.lib",
                "swscale.lib",
            ]
        )
    else:
        env.Append(LINKFLAGS=["-static"])

        libs = [
            "-lSvtAv1Enc",
            "-laom",
            "-lmp3lame",
            "-lopus",
            "-lvorbis",
            "-lvorbisenc",
            "-logg",  # must be after '-lvorbis'
            "-lvpx",
            "-lx264",
            "-lx265",
            "-ladvapi32",
            "-latomic",
            "-lbcrypt",
            "-lbz2",
            "-lgcc",
            "-lgcc_s",
            "-lgdi32",
            "-liconv",
            "-llzma",
            "-lm",
            "-lmfplat",
            "-lole32",
            "-loleaut32",
            "-lpsapi",
            "-lsecur32",
            "-lshell32",
            "-lshlwapi",
            "-lstdc++",
            "-lstrmiids",
            "-luser32",
            "-luuid",
            "-lvfw32",
            # '-lws2',
            "-lws2_32",
            "-lz",
            "-lpthread",  # must be after everything
        ]

        if os_platform.system().lower() != "windows":
            # Some distros don't have certain libraries
            conf = Configure(env)
            found_libs = [
                lflag for lflag in libs if conf.CheckLib(lflag.removeprefix("-l"))
            ]

            if not all(lib in found_libs for lib in libs):
                print("Ignoring libraries not found.")
            conf.env.Append(LIBS=LIBS_COMMON + found_libs)
            env = conf.Finish()
        else:
            env.Append(LIBS=LIBS_COMMON + libs)

elif "macos" in platform:
    env.Append(LINKFLAGS=["-Wl,-rpath,@loader_path"], LIBS=LIBS_COMMON)

    if arch == "arm64":
        env.Append(CCFLAGS=["-arch", "arm64"], LINKFLAGS=["-arch", "arm64"])
    else:
        env.Append(CCFLAGS=["-arch", "x86_64"], LINKFLAGS=["-arch", "x86_64"])


src = Glob("gde_gozen/*.cpp")
sharedlib = env.SharedLibrary(libpath, src)
Default(sharedlib)
